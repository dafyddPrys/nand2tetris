// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:

   // zx
   Mux16(a=x, b=false, sel=zx, out=outzx);
   // nx
   Not16(in=outzx, out=notoutzx);
   Mux16(a=outzx, b=notoutzx, sel=nx, out=outnx);

   // zy
   Mux16(a=y, b=false, sel=zy, out=outzy);
   // ny
   Not16(in=outzy, out=notoutzy);
   Mux16(a=outzy, b=notoutzy, sel=ny, out=outny);

   // potential orxy
   Add16(a=outnx, b=outny, out=addxy);
   // potential andxy
   And16(a=outnx, b=outny, out=andxy);
   // Select which one of the above based on f
   // if f = 0 then andxy else addxy
   Mux16(a=andxy, b=addxy, sel=f, out=fxy);

   // potential notfxy
   Not16(in=fxy, out=notfxy);
   // if no then notfxy else fxy
   Mux16(a=fxy, b=notfxy, sel=no, out=outno);
   // need this so we can use the out pin for zr, ng
   Or16(a=outno, b=false, out=out);

   // set zr
   // created Or16Way myself
   // needed to split into separate chip so we could access the
   // sub-bus of outno
   Or16Way(in=outno, out=iszero);
   Not(in=iszero, out=zr);

   // set ng
   // created IsNegative16 myself
   IsNegative16(in=outno, out=ng);

}